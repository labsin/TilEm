Name "@PACKAGE_NAME@"
OutFile "@PACKAGE_TARNAME@-@PACKAGE_VERSION@.exe"
SetCompressor /solid lzma

!define MULTIUSER_EXECUTIONLEVEL Highest
!define MULTIUSER_MUI
!define MULTIUSER_INSTALLMODE_INSTDIR "@PACKAGE_NAME@"
!include "MultiUser.nsh"
!include "MUI2.nsh"

Var StartMenuFolder

!define MUI_FINISHPAGE_RUN ""
!define MUI_FINISHPAGE_RUN_TEXT "Create a desktop shortcut"
!define MUI_FINISHPAGE_RUN_FUNCTION desktopicon

!insertmacro MUI_PAGE_WELCOME
!insertmacro MULTIUSER_PAGE_INSTALLMODE
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE checkinstalldir
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "English"

Function .onInit
	!insertmacro MULTIUSER_INIT
FunctionEnd

Function un.onInit
	UserInfo::GetAccountType
	Pop $MultiUser.Privileges
	ReadINIStr $0 "$INSTDIR\uninstall.ini" "Uninstall" "InstallMode"
	${if} $0 == "AllUsers"
		call un.MultiUser.InstallMode.AllUsers
	${else}
		call un.MultiUser.InstallMode.CurrentUser
	${endif}
FunctionEnd

Function checkinstalldir
	${if} ${FileExists} "$INSTDIR\uninstall.exe"
	${andif} ${FileExists} "$INSTDIR\uninstall.ini"
		MessageBox MB_OKCANCEL|MB_ICONINFORMATION \
			"TilEm is already installed in this directory, \
			and will be upgraded.  Please ensure that \
			TilEm is not running before continuing." \
			IDOK installdirok
		Abort
		installdirok:
	${endif}
FunctionEnd

Function desktopicon
	SetShellVarContext current
	CreateShortCut "$DESKTOP\TilEm.lnk" "$INSTDIR\bin\tilem2.exe"
FunctionEnd

Section
	SetOutPath "$INSTDIR"

	${if} ${FileExists} "$INSTDIR\uninstall.exe"
	${andif} ${FileExists} "$INSTDIR\uninstall.ini"
		ExecWait '"$INSTDIR\uninstall.exe" /S _?=$INSTDIR'
	${endif}

	!include "files.nsi"

	${if} $MultiUser.InstallMode == "CurrentUser"
	${andifnot} ${FileExists} "$INSTDIR\etc\tilem2\config.ini"
		CreateDirectory "$INSTDIR\etc\tilem2"
		${if} ${FileExists} "$LOCALAPPDATA\tilem2\config.ini"
			CopyFiles "$LOCALAPPDATA\tilem2\*" "$INSTDIR\etc\tilem2"
                ${else}
			FileOpen $0 "$INSTDIR\etc\tilem2\config.ini" w
			FileWrite $0 "$\n"
			FileClose $0
		${endif}
	${endif}

	!insertmacro MUI_STARTMENU_WRITE_BEGIN Application
		CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
		CreateShortCut "$SMPROGRAMS\$StartMenuFolder\TilEm.lnk" "$INSTDIR\bin\tilem2.exe"
		CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall TilEm.lnk" "$INSTDIR\uninstall.exe"
		WriteINIStr "$INSTDIR\uninstall.ini" "Uninstall" "StartMenuFolder" $StartMenuFolder
	!insertmacro MUI_STARTMENU_WRITE_END

	WriteINIStr "$INSTDIR\uninstall.ini" "Uninstall" "InstallMode" $MultiUser.InstallMode
	WriteUninstaller "$INSTDIR\uninstall.exe"
SectionEnd

Section "Uninstall"
	!include "rmfiles.nsi"
	!include "rmdirs.nsi"

	ReadINIStr $StartMenuFolder "$INSTDIR\uninstall.ini" "Uninstall" "StartMenuFolder"
	${if} $StartMenuFolder != ""
		Delete "$SMPROGRAMS\$StartMenuFolder\TilEm.lnk"
		Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall TilEm.lnk"
		RmDir "$SMPROGRAMS\$StartMenuFolder"
	${endif}

	SetShellVarContext current
	Delete "$DESKTOP\TilEm.lnk"

	Delete "$INSTDIR\uninstall.ini"
	Delete "$INSTDIR\uninstall.exe"
	RmDir "$INSTDIR"
SectionEnd
